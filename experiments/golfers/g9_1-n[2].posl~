St_rb001 := cStrategy
oModule: OM_S, OM_V, OM_SS, OM_D;
oChannel: OCh;
{
<S>
    OP.Cyc (BE.LoopBnd 50)
    <S>
        OP.|->
        OM_S
        <S>
            OP.Cyc ( BE.LoopBnd 5000 )
            <S>
                OP.|->
                <S>
                    OP.|->
                    <S>
                        OP.|->
                        OM_V
                        <S>
                            OP.|->
                            <S>
                                OP.? (BE.SCI 5)
                                OM_SS
                                OM_SS.Rand
                            </S>
                            <S>
                                OP.? (BE.LoopBnd 5)
                                OM_D
                                <S> OP.Min OM_D OCh (Ch1) </S>
                            </S>
                        </S>
                    </S> OMS.IterCounter
                </S> OMS.TimeCounter
            </S>
        </S>
    </S>
</S>
};

St_sb001 := cStrategy
        oModule: OM_S, OM_V, OM_SS, OM_D;
{
<S>
    OP.Cyc ( BE.LoopBnd 50 )
    <S>
        OP.|->
        OM_S
        <S>
            OP.Cyc ( BE.LoopBnd 5000 )
            <S>
                OP.|->
                <S>
                    OP.|->
                    <S>
                        OP.|->
                        OM_V
                        <S>
                            OP.? (BE.SCI 5)
                            <S> OP.|-> OM_SS <S> OP.OSend (send_1) OM_D </S> </S>
                            <S> OP.|-> OM_SS.Rand OM_D </S>
                        </S>
                    </S> OMS.IterCounter
                </S> OMS.TimeCounter
            </S>
        </S>
    </S>
</S>
};

solver_sender_partial := solver(20)
{
    cStrategy: St_sb001;
    oModule: OM_S.PermBlocks, OM_V.TournPartialWeek(9-4-2), OM_SS.FirstImpr, OM_D.AlwImpr;
};

solver_receiver := solver(20)
{
    cStrategy: St_rb001;
    oModule: OM_S.PermBlocks, OM_V.AS_Golfers, OM_SS.FirstImpr, OM_D.AlwImpr;
    oChannel: OCh.Sol_Last;
};

connections:

[solver_sender_partial.send_1(20)] OP.#-> [solver_receiver.Ch1(20)];


